buildscript {
    ext.kotlin_version = '1.9.0'
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

rootProject.buildDir = '../build'
subprojects {
    project.buildDir = "${rootProject.buildDir}/${project.name}"

    // Workaround for plugins expecting 'flutter' property to be present
    if (project.name != 'app') {
        ext.flutter = [
            minSdkVersion: 21,
            targetSdkVersion: 34,
            compileSdkVersion: 34,
            ndkVersion: "25.1.8937393"
        ]
    }

    afterEvaluate { project ->
        if (project.hasProperty("android")) {
            project.android {
                compileSdkVersion 34
                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_17
                    targetCompatibility JavaVersion.VERSION_17
                }
            }
            project.tasks.withType(JavaCompile) {
                options.compilerArgs << "-Xlint:-options"
            }
            project.tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
                kotlinOptions {
                    jvmTarget = "17"
                }
            }
        }
    }
}

subprojects {
    project.evaluationDependsOn(':app')
}



tasks.register("clean", Delete) {
    delete rootProject.buildDir
}


