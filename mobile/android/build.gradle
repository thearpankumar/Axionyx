buildscript {
    ext.kotlin_version = '2.3.0'
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

rootProject.buildDir = '../build'
subprojects {
    project.buildDir = "${rootProject.buildDir}/${project.name}"

    // Workaround for plugins expecting 'flutter' property to be present
    if (project.name != 'app') {
        ext.flutter = [
            minSdkVersion: 21,
            targetSdkVersion: 35,
            compileSdkVersion: 35,
            ndkVersion: "27.0.12077973"
        ]
    }

    afterEvaluate { project ->
        if (project.hasProperty("android")) {
            project.android {
                compileSdkVersion 35
                if (project.android.hasProperty('ndkVersion')) {
                    ndkVersion "27.0.12077973"
                }
                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_17
                    targetCompatibility JavaVersion.VERSION_17
                }
            }
            project.tasks.withType(JavaCompile) {
                options.compilerArgs << "-Xlint:-options"
            }
            project.tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
                kotlinOptions {
                    jvmTarget = "17"
                }
            }
        }
    }
}

subprojects {
    project.evaluationDependsOn(':app')
}



tasks.register("clean", Delete) {
    delete rootProject.buildDir
}


